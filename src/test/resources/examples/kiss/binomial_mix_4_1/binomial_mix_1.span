# NAME: Binomial Mix Protocol
# PARAMETERS: 4 parties, 1 round

.metadata

    version : 1.0;
    recipe depth : 1;

.constants

    fraction p : 1/6;

.signature

    sort : PKey, SKey, Nonce, Message;
    subsort : PKey, SKey, Nonce < Message;

    # equational theory variables
    variables : m | Message;
    variables : k, r | Nonce;

    # input variables
    variables : y1, y2, y3, y4, y5 | Message;

    # input guard variables
    variables : message | Message;
    variables : nonce | Nonce;

    function : aenc | Message Nonce PKey -> Message;
    function : adec | Message SKey -> Message;
    function : pk | Nonce -> PKey;
    function : sk | Nonce -> SKey;

    public names : a | Nonce;

    private names : m1, m2, m3, m4 | Message;
    private names : r1, r2, r3, r4, r5, r6, r7, r8 | Nonce;
    private names : k1, k2, k3, k4, km | Nonce;
    private names : s | Nonce;

.rewrites

    rewrite : adec(aenc(m,r,pk(k)), sk(k)) -> m;

.roles

    role :  {0} [T] out(1 -> pk(km));

    role :  {1} [T] out(1 -> aenc(aenc(m1, r1, pk(k1)), r2, pk(km))) .
            {2} [T] out(1 -> aenc(aenc(m2, r3, pk(k2)), r4, pk(km))) .
            {3} [T] out(1 -> aenc(aenc(m3, r5, pk(k3)), r6, pk(km))) .
            {4} [T] out(1 -> aenc(aenc(m4, r7, pk(k4)), r8, pk(km)));

    role:   {5} in(y1{aenc(aenc(message, nonce, pk(nonce)), nonce, pk(km))}) .
            {5} in(y2{aenc(aenc(message, nonce, pk(nonce)), nonce, pk(km))}) .
            {5} in(y3{aenc(aenc(message, nonce, pk(nonce)), nonce, pk(km))}) .
            {5} in(y4{aenc(aenc(message, nonce, pk(nonce)), nonce, pk(km))}) .
            {5} [ y1 != y2 & y1 != y3 & y1 != y4 &
                  y2 != y3 & y2 != y4 & y3 != y4 ] out(1/4 -> adec(y1, sk(km)) +
                                                       3/4 -> T) .
            {5} [ y1 != y2 & y1 != y3 & y1 != y4 &
                  y2 != y3 & y2 != y4 & y3 != y4 ] out(1/4 -> adec(y2, sk(km)) +
                                                       3/4 -> T) .
            {5} [ y1 != y2 & y1 != y3 & y1 != y4 &
                  y2 != y3 & y2 != y4 & y3 != y4 ] out(1/4 -> adec(y3, sk(km)) +
                                                       3/4 -> T) .
            {5} [ y1 != y2 & y1 != y3 & y1 != y4 &
                  y2 != y3 & y2 != y4 & y3 != y4 ] out(1/4 -> adec(y4, sk(km)) +
                                                       3/4 -> T);

    role:   {6} in(y5{aenc(m2, r3, pk(k2))}) .
            {6} [ y5 == aenc(m2, r3, pk(k2)) ] out(1 -> s);

.safety
    secrecy : s >= 1/2;